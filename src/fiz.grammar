@top Recipe { statement* }

statement { ( Call | Alias | Assignment | StyleTagDeclaration ) semi }

Call { Identifier ArgList StyleArgList? }

ArgList { "(" ( value ("," value)* )? ")" }

value[@isGroup=Value] { Variable | Call }

Variable { Identifier }

Alias { lhsVariable "=" Variable }

Assignment { lhsVariable ("," lhsVariable)* "=" Call }

semi { ";" | insertSemi }

NumberLiteral { numberLiteralInner Unit? }

lhsVariable { Variable StyleArgList? }

styleValue[@isGroup=StyleValue] { ColorLiteral | NumberLiteral | StringLiteral }

StyleDeclaration { Identifier ":" styleValue (","? styleValue)* semi }

StyleTagDeclaration { StyleTag StyleArgList }

StyleTag { "#" Identifier }

StyleArgList { "{" (StyleDeclaration | StyleTag)* "}" }

@skip { spaces | newline | LineComment | BlockComment }

@context trackNewline from "./tokens.js"

@external tokens unitToken from "./tokens" { Unit }

@tokens {
  spaces[@export] { $[\u0009 \u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff]+ }
  newline[@export] { $[\r\n\u2028\u2029] }

  LineComment { "//" ![\n]* }

  BlockComment { "/*" blockCommentRest }

  blockCommentRest { ![*] blockCommentRest | "*" blockCommentAfterStar }

  blockCommentAfterStar { "/" | "*" blockCommentAfterStar | ![/*] blockCommentRest }

  identifierChar { std.asciiLetter | $[_$\u{a1}-\u{10ffff}] }

  word { identifierChar (identifierChar | std.digit)* }

  Identifier { word }

  hexDigit { std.digit | $[a-fA-F] }

  ColorLiteral { "#" hexDigit hexDigit hexDigit (hexDigit (hexDigit hexDigit (hexDigit hexDigit)?)?)? }

  numberLiteralInner { ("+" | "-")? (std.digit+ ("." std.digit*)? | "." std.digit+) (("e" | "E") ("+" | "-")? std.digit+)? }

  StringLiteral { "\"" (!["\n\\] | "\\" _)* "\"" | "'" (!['\n\\] | "\\" _)* "'" }

  "="[@name=Eq]

  @precedence { spaces, newline, Identifier }

  "#"

  ":"

  "(" ")" "{" "}"

}

@external tokens insertSemicolon from "./tokens" { insertSemi }

@external propSource fizHighlighting from "./highlight"

@detectDelim
