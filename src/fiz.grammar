@top Recipe { fullStatement* }

fullStatement { statement semi }

statement[@isGroup=Statement] { Call | Alias | Assignment }

ArgList { "(" ( value ("," value)* )? ")" }

Call { Identifier ArgList }

value[@isGroup=Value] { Variable | Call }

Variable { Identifier }

Alias { Variable "=" Variable }

Assignment { Variable ("," Variable)* "=" Call }

semi { ";" | insertSemi }

@skip { spaces | newline | LineComment | BlockComment }

@context trackNewline from "./tokens.js"

@tokens {
  spaces[@export] { $[\u0009 \u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff]+ }
  newline[@export] { $[\r\n\u2028\u2029] }

  LineComment { "//" ![\n]* }

  BlockComment { "/*" blockCommentRest }

  blockCommentRest { ![*] blockCommentRest | "*" blockCommentAfterStar }

  blockCommentAfterStar { "/" | "*" blockCommentAfterStar | ![/*] blockCommentRest }

  identifierChar { std.asciiLetter | $[_$\u{a1}-\u{10ffff}] }

  word { identifierChar (identifierChar | std.digit)* }

  Identifier { word }

  "="[@name=Eq]

  @precedence { spaces, newline, Identifier }

  "(" ")"

}

@external tokens insertSemicolon from "./tokens" { insertSemi }

@detectDelim
